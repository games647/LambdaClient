plugins {
    id 'java'
    id 'de.undercouch.download' version '3.1.1'
}

group 'com.github.games647'
version '1.0-SNAPSHOT'

//downloads a new version of the wrapper with the /gradle wrapper command
task wrapper(type: Wrapper) {
  gradleVersion = '3.1'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

sourceCompatibility = 1.8

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", ".mcp/src/minecraft"]
        }
    }
}

//dependencies
repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: '.mcp/jars/libraries/', include: '**/*.jar')
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

//tasks
import de.undercouch.gradle.tasks.download.Download
task downloadMcp(type: Download) {
    def version = 931
    download {
        src 'http://www.modcoderpack.com/website/sites/default/files/releases/mcp' + version + '.zip'
        dest 'mcp' + version + '.zip'
    }

    copy {
        def zipFile = file('mcp' + version +'.zip')
        def outputDir = file(".mcp")

        from zipTree(zipFile)
        into outputDir
    }
}

task decompile(type: Exec) {
    def workingDir = ".mcp"
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', 'decompile.bat'
    } else {
        commandLine 'sh', '-c', 'decompile.sh'
    }
}

/**
 * Branch: Master => Minecraft code with changes from the latest applyPatch
 * Tag: First -> Minecraft code without changes
 */
task updatePatch << {
    def mcpDir = ".mcp/src/minecraft"

    //reset only the git history to the first commit in order to output only patch file
    exec {
        workingDir mcpDir
        commandLine 'cmd', '/c', 'git diff First > ../../../Minecraft-Patch.diff'
    }

    //create a merge commit
    exec {
        workingDir mcpDir
        commandLine 'cmd', '/c', 'git reset --soft First'
    }

    //commit those changes with an anonymous user
    exec {
        workingDir mcpDir
        commandLine 'cmd', '/c', 'git commit --author="Auhtor <author@example.com>" -a -m "Minecraft patch"'
    }
}

task applyPatch << {
    def mcpDir = ".mcp/src/minecraft"

    //setup the custom git history for the mc code
    if (!new File(mcpDir, ".git").exists()) {
        //Make the first commit
        exec {
            workingDir mcpDir
            commandLine 'cmd', '/c', 'git init'
        }

        exec {
            workingDir mcpDir
            commandLine 'cmd', '/c', 'git add .'
        }

        exec {
            workingDir mcpDir
            commandLine 'cmd', '/c', 'git commit -m "First"'
        }

        //save this point in order to restore it to make a complete diff every time
        exec {
            workingDir mcpDir
            commandLine 'cmd', '/c', 'git tag -a -m "-" First'
        }
    }

    //apply the patches
    exec {
        workingDir mcpDir
        commandLine 'cmd', '/c', 'git apply ../../../Minecraft-Patch.diff'
    }
}

task run(type: Exec) {
    def workingDir = ".mcp"
    if (System.getProperty('os.name').toLowerCase().contains('windows')) {
        commandLine 'cmd', '/c', 'startclient.bat'
    } else {
        commandLine 'sh', '-c', 'startclient.sh'
    }
}

task install(type: Copy) {
    def fromFile = file('.mcp/jars/versions/1.10')
    def clientName = project.name + '-' + version;
    def outputDir = new File(getMinecraftFolder(), "versions/" + clientName);

    from fromFile
    into outputDir
    rename {
        String fileName -> fileName.replace("1.10", clientName)
    }

    filter {
        it.replace('"id": "1.10"', '"id": "' + clientName + '"')
    }
}

def File getMinecraftFolder() {
    String userHome = System.getProperty("user.home", ".");
    File result;

    def osName = System.getProperty('os.name').toLowerCase()
    if (osName.contains('windows')) {
        String applicationData = System.getenv("APPDATA")
        String folder = applicationData != null ? applicationData : userHome
        result = new File(folder, ".minecraft/")
    } else if (osName.contains('macos')) {
        result = new File(userHome, "Library/Application Support/minecraft")
    } else if (osName.contains('linux') || osName.contains('solaris')) {
        result = new File(userHome, ".minecraft/")
    } else {
        result = new File(userHome, "minecraft/")
    }

    return result
}